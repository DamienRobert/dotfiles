#compdef checkmail dofor dm powersave get_authinfo raspberry vcs

case $service in

  checkmail )
    _arguments -s -S \
  {-c,--color}"[Color output (on by default)]" \
  "--notify[Send output to notification]" \
  "--check[specify list of maildir to check]:maildir:_files -/" \
  "-q[quiet]" 
  ;;

  dofor ) 
    _arguments \
    ':Command:_command_names -e' \
    '*::arguments: _normal'
    ;;

  dm)
    _arguments \
      '1:Action:(logout dialog lock suspend hibernate halt poweroff)'
    ;;

  powersave )
    local -a values
    #we need an extra level of quoting here:
    values=("extratrue:'maximal power save'"
            "true:'activate power save'"
            "false:'desactivate power save'"
            "status:'see powersave status'"
            "usb:'set powervalue of usb bus'"
            "usbstatus:'show usb bus powervalue'")
    if [[ $CURRENT -gt 2 && $words[2] = "usb" ]]; then
      (( CURRENT-- ))
      _arguments \
        ':usb power value:(on auto suspend)'
    else
    _arguments \
      ":Powersave value:(($values))"
    fi
  ;;

  get_authinfo )
    _arguments \
      '-connect[only try to connect]' \
      '-env[use ~/.gpg-agent-info]' \
      '-auth[allow to ask a passphrase]' \
      '-key[allow to ask a passphrase]:key file:_files' \
      '-gpgopt[add gpg option]:gpg option' \
      '-gpgagentopt[add gpg-agent option]:gpg-agent option' \
      - Actions \
      '1:Action:(start exec startexec wait)' \
      '2:Command:_command_names -e' \
      '*:::arguments: _normal' \
      - Credentials \
        '1:Credential:(pass)' \
        #'2:machine:'  \
        #'3:login:'  \
        #'4:port:'
  ;;

  raspberry)
  if [[ $CURRENT -gt 2 && $words[2] = "audio" ]]; then
      shift words
      (( CURRENT-- ))
      _arguments '1:Value:((0\:auto 1\:analog 2\:hdmi))'
   else
      _arguments '1:Action:(audio mpd)'
   fi
  ;;

  vcs)
    _arguments \
    '1:Action:(list update)' \
    '*:VCS folders:_files -/'
  ;;
esac
