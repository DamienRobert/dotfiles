#compdef clean_tmp comment copyerror daemonize get_http_links mylatex reduce_pdf_size rrd set_title unlock word_to_code.pl

case $service in

  clean_tmp )
    _arguments \
    '-d[Directory to clean (default ~/tmp)]:Directory:_files -/'
    ;;

  comment)
    _arguments \
    '-u[uncomment]' \
    '*:Files:_files'
    ;;

  copyerror)
    _arguments \
    '1:Orig file:_files' \
    '2:Dest dir:_files -/'
    ;;

  daemonize)
    _arguments \
    '-c[ionice class]:ionice class:' \
    '-p[ionice priority]:ionice priority:' \
    '-n[nice priority]:nice priority:' \
    ':Command:_command_names -e' \
    '*::arguments: _normal'
    ;;

  ethconfig ) 
    _arguments \
    ':IP:' \
    ':Broadcast:' \
    ':Netmask:' \
    ':Gateway:' \
    ':Nameserver:'
    ;;

  get_http_links ) 
  if [[ $CURRENT -gt 2 && $words[2] = "-dl" ]]; then
      shift words
      (( CURRENT-- ))
      _arguments \
       ':links file:_files' \
       ':dest dir:_files -/'
  else
    _arguments  \
    '-dl[download links]' \
    '*:url:_urls'
  fi
    ;;

  mylatex)
    _arguments \
    '-bib[use bibtex]' \
    '-lua[use luatex]' \
    '-xetex[use xetex]' \
    '-synctex[use synctex]' \
    '*:: :_tex'
    ;;

  reduce_pdf_size)
    _arguments \
    '*:pdf file:_files -g *.pdf'
    ;;

  rrd)
    _arguments \
    '*:directory:_files -/'
    ;;

  set_title)
    _arguments \
    '-hs[only set hardstatus]' \
    '-s[only set status]' \
    '*:title:'
    ;;

  unlock)
    if [[ $CURRENT -gt 2 && $words[2] = "-s" ]]; then
        shift words
        (( CURRENT-- ))
        _command
    else
      _arguments \
      '-l[lock]' \
      '-s[unlock sudo]' \
      '--services[launch unlock target (true by default)]:value:(true false)'
    fi
    ;;

  word_to_code.pl)
    _arguments \
    '1:message:'
    ;;
esac
